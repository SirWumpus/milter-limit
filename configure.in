#######################################################################
#	Thus it begins...
#######################################################################

AC_INIT(milter-limit, 0.16, [Anthony Howe <achowe@snert.com>])

dnl The autoconf version I learned to deal with.
AC_PREREQ(2.59)

dnl Yes I wrote this mess.
SNERT_INIT(MILTER, [Copyright 2004, 2019 by Anthony Howe. All rights reserved.])

AC_SUBST(snert_milter_t_equate, 'C:10s;S:10s;R:10s;E:10s')

AC_SUBST(rel_incdir, '../../include')
AC_SUBST(rel_libdir, '../../lib')
AC_SUBST(rel_tardir, '../../../..')

#######################################################################
#	Setup paths
#######################################################################

# The total perspective vortex "You are here..."
AC_CONFIG_SRCDIR($PACKAGE_TARNAME.c)

#######################################################################
#	Auxliary files to be modified later
#######################################################################

AC_CONFIG_HEADERS(config.h)
AC_CONFIG_FILES(manual.shtml)
AC_CONFIG_FILES(startup.sh)
AC_CONFIG_FILES(sample.mc)
AC_CONFIG_FILES(install.sh)
AC_CONFIG_FILES(makefile)

#######################################################################
#	Configuration options: settings
#######################################################################

SNERT_OPTION_ENABLE_CACHE_FILE
SNERT_OPTION_ENABLE_CACHE_TYPE(default)
SNERT_OPTION_ENABLE_DEBUG
SNERT_OPTION_ENABLE_RUN_USER(milter)
SNERT_OPTION_ENABLE_RUN_GROUP(milter)
SNERT_OPTION_ENABLE_PID('${localstatedir}/run/milter/${PACKAGE_NAME}.pid')
SNERT_OPTION_ENABLE_SOCKET('${localstatedir}/run/milter/${PACKAGE_NAME}.socket')

AC_ARG_ENABLE(header-buffer-size,
	[AC_HELP_STRING([--enable-header-buffer-size=bytes],[specifiy the default header buffer size in bytes, default is 8192 bytes])],
	[
		AC_DEFINE_UNQUOTED(DEFAULT_HEADER_BUFFER_SIZE, [$enableval])
	]
)

#######################################################################
#	Configuration options: packages
#######################################################################

SNERT_OPTION_WITH_SENDMAIL

#######################################################################
#	Load LibSnert Configuration
#######################################################################

if test -f '../lib/version.sh' ; then
	snert_libsnert=yes
	. '../lib/version.sh'

	AC_SUBST(CC)
	AC_SUBST(CC_O)
	AC_SUBST(CC_E)
	AC_SUBST(CFLAGS)
	AC_SUBST(LDFLAGS)
	AC_SUBST(ARCHIVE)
	AC_SUBST(COMPILE)
	AC_SUBST(RANLIB)
	AC_SUBST(LIBEXT)
	AC_SUBST(OBJEXT)
	AC_SUBST(EXEEXT)
	AC_SUBST(AUTOCONF)
	AC_SUBST(MD5SUM)
	AC_SUBST(TAR_I)

	AC_SUBST(LIBS_RT)
	AC_SUBST(HAVE_LIB_SEM)

	AC_SUBST(CFLAGS_SNERT)
	AC_SUBST(CPPFLAGS_SNERT)
	AC_SUBST(LDFLAGS_SNERT)
	AC_SUBST(LIBS_SNERT)

	AC_SUBST(CFLAGS_DB)
	AC_SUBST(LDFLAGS_DB)
	AC_SUBST(HAVE_LIB_DB)

	AC_SUBST(CFLAGS_MILTER)
	AC_SUBST(LDFLAGS_MILTER)
	AC_SUBST(LIBS_MILTER)

	AC_SUBST(CFLAGS_SQLITE3)
	AC_SUBST(LDFLAGS_SQLITE3)
	AC_SUBST(LIBS_SQLITE3)

	AC_SUBST(CFLAGS_PTHREAD)
	AC_SUBST(LDFLAGS_PTHREAD)
	AC_SUBST(HAVE_LIB_PTHREAD)

	if test "${HAVE_LIB_PTHREAD:-no}" = 'no' -a "${CFLAGS_PTHREAD:-no}" = 'no' ; then
		echo
		echo '***************************************************************'
		echo "       Sendmail milters require POSIX thread support."
		echo '***************************************************************'
		echo
		exit 1
	fi
	if test "X${HAVE_LIB_MILTER}" = 'X' ; then
		echo
		echo '***************************************************************'
		echo "Oi! You must install Sendmail's libmilter and associated C"
		echo 'headers first, then reconfigure and rebuild LibSnert again,'
		echo 'before reconfiguring and building this milter.'
		echo '***************************************************************'
		echo
		exit 1
	fi
else
	snert_libsnert=no
	echo
	echo '***************************************************************'
	echo 'Oi! You must download, configure, and build LibSnert first!'
	echo '***************************************************************'
	echo
fi

#######################################################################
#	Generate output.
#######################################################################

SNERT_FINI

echo
echo "Generating files..."
echo
AC_OUTPUT()

echo
echo $PACKAGE_NAME/$package_major.$package_minor.$package_build
echo $package_copyright
echo
AC_MSG_RESULT([  Platform..............: $platform $CC])
AC_MSG_RESULT([  LibSnert..............: $snert_libsnert])
AC_MSG_RESULT([  --prefix..............: $prefix])
AC_MSG_RESULT([  --datadir.............: $datadir])
AC_MSG_RESULT([  --localstatedir.......: $localstatedir])
AC_MSG_RESULT([  --exec_prefix.........: $exec_prefix])
AC_MSG_RESULT([  --sbindir.............: $sbindir])
AC_MSG_RESULT([  --libexecdir..........: $libexecdir])
AC_MSG_RESULT([  --enable-run-user.....: $enable_run_user])
AC_MSG_RESULT([  --enable-run-group....: $enable_run_group])
AC_MSG_RESULT([  --enable-pid..........: $snert_pid_file])
AC_MSG_RESULT([  --enable-socket.......: $snert_socket_file])
AC_MSG_RESULT([  --enable-cache-file...: $snert_cache_file])
AC_MSG_RESULT([  --enable-cache-type...: $enable_cache_type])
AC_MSG_RESULT([  --with-sendmail.......: $with_sendmail])
AC_MSG_RESULT([  CFLAGS................: $CFLAGS])
AC_MSG_RESULT([  LDFLAGS...............: $LDFLAGS])
AC_MSG_RESULT([  LIBS..................: $LIBS])
echo


